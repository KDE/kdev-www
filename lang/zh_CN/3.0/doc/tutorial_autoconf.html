<?php module_head("Tutorial about autoconf and KDevelop");?>
<p>KDevelop系统使用了GNU
<a href="http://www.gnu.org/software/autoconf/">autoconf</a>
<i>autoconf</i> 是用来生成Makefiles和配置(configure)脚本的工具.它功能强大,灵活,
同时也是不易理解,即使是要向简单依赖关系加入一个新任务,也是如此.</p>

<p>The KDE 的<b>admin/</b> 目录下有一些配置文件和脚本,它们可以来简化这个配置过程. 在KDevelop的autoconf设置中, 最重要的一个
命令是<b>make -f admin/Makefile.common</b>
(KDevelp 2.x还要用<b>rm config.guess</b>).
这个命令用来重新生成autoconf的设置,同时删去config.guess,清除config cache.</p>

<p>改变autconf设置的一种常见情况是,比如要加入依赖KDevelop GUI之外的一些共享库. 如libxml2. 可以在KDevelop 选项(option)
中的CPPFLAGS和LDFLAGS加入命令
<b>`xml2-config --cflags`</b> and <b>`xml2-config --libs`</b> 是很容易的. 但是这只在KDevelop环境
中是有效的,如果你要创建source(.tar.gz) distribution , 那这个命令就不会包含在distribution中.</p>

<p>加入这个依赖关系是可以通过autoconf, 而这需要两个文件要被改动: <b>configure.in.in</b> 和
<b>Makefile.am</b>.  是在project第一次生成时就创建的一个模板, KDevelop不会修改这个文件. Makefile.am也是个模板,但是KDevelp 2.x
会覆盖Makefile.am中的某些部分,  最然被覆盖的地方会被清楚的标明, 其它地方保留.  但在KDevelop 3.x中没有这个限制. <p>

<p>让我们看看这几个文件干了什么</p>

<p><b>configure.in.in</b> 被
<a href="http://www.gnu.org/software/m4/m4.html">m4</a>一个GNU的宏处理语言所处理. 这个语言能生成<i>sendmail</i>的配置文件,和其他很多事情. 虽然它值
的学习,在这里就不详细解释了. 下面这段代码可以加到configure.in.in中, 用来加入libxml2的依赖关系:<p>

<pre>
dnl -----------------------------------------------------------------------
dnl libxml2 check from http://websvn.kde.org/*checkout*/branches/KDE/3.5/kdelibs/kdoctools/configure.in.in

KDE_FIND_PATH(xml2-config, XML_CONFIG, [${prefix}/bin ${exec_prefix}/bin /usr/local/bin /opt/local/bin], [
  AC_MSG_WARN([Could not find libxml2 anywhere, check ftp://xmlsoft.org/ for libxml >= 2.4.8.
  HELP_SUBDIR=
])

if test -n "$XML_CONFIG"; then
  vers=`$XML_CONFIG --version 2>/dev/null | sed -e 's/libxml //' | awk 'BEGIN { FS = "."; } { printf "%d", ($1 * 1000 + $2) * 1000 + $3;}'`
  if test -n "$vers" &amp;&amp; test "$vers" -ge 2005000
  then
     LIBXML_LIBS="`$XML_CONFIG --libs`"
     LIBXML_RPATH=
     for args in $LIBXML_LIBS; do
          case $args in
            -L*)
               LIBXML_RPATH="$LIBXML_RPATH $args"
               ;;
          esac
     done
     LIBXML_RPATH=`echo $LIBXML_RPATH | sed -e "s/-L/-R/g"`
     LIBXML_CFLAGS="`$XML_CONFIG --cflags`"
  else
     AC_MSG_WARN([You need at least libxml 2.5.0])
     HELP_SUBDIR=
  fi
fi

AC_SUBST(LIBXML_LIBS)
AC_SUBST(LIBXML_CFLAGS)
dnl -----------------------------------------------------------------------
</pre>

<p>解释如下:</p>

<pre>
KDE_FIND_PATH(xml2-config, XML_CONFIG, [${prefix}/bin ${exec_prefix}/bin /usr/local/bin /opt/local/bin], [
  AC_MSG_WARN([Could not find libxml2 anywhere, check ftp://xmlsoft.org/ for libxml >= 2.4.8.
  HELP_SUBDIR=
])
</pre>

<p>这段代码在预定义的路径中寻找<i>xml2-config</i> 程序. 找到的路径放在变量
<b>XML_CONFIG</b> 如果没找到xml-config
XML_CONFIG保持未赋值状态,同时打印出错消息.</p>

<pre>
if test -n "$XML_CONFIG"; then
  vers=`$XML_CONFIG --version 2>/dev/null | sed -e 's/libxml //' | awk 'BEGIN { FS = "."; } { printf "%d", ($1 * 1000 + $2) * 1000 + $3;}'`
  if test -n "$vers" &amp;&amp; test "$vers" -ge 2005000
  then
</pre>
<p>如果XML_CONFIG被赋值, 那么确认正确的路径, 得到版本号,同时检查版本是否大于2.5.0.</p>

<pre>
     LIBXML_LIBS="`$XML_CONFIG --libs`"
     LIBXML_RPATH=
     for args in $LIBXML_LIBS; do
          case $args in
            -L*)
               LIBXML_RPATH="$LIBXML_RPATH $args"
               ;;
          esac
     done
     LIBXML_RPATH=`echo $LIBXML_RPATH | sed -e "s/-L/-R/g"`
     LIBXML_CFLAGS="`$XML_CONFIG --cflags`"
</pre>

<p>定义变量<b>LIBXML_LIBS</b>, <b>LIBXML_RPATH</b>, 和 <b>LIBXML_CFLAGS</b>.</p>

<pre>
  else
     AC_MSG_WARN([You need at least libxml 2.5.0])
     HELP_SUBDIR=
  fi
</pre>

<p>如果libxml2被找到,但是版本太老了,那么打印出错消息.</p>

<pre>
fi

AC_SUBST(LIBXML_LIBS)
AC_SUBST(LIBXML_CFLAGS)
</pre>

<p>最后让变量<b>LIBXML_LIBS</b> 和 <b>LIBXML_CFLAGS</b>
能被其他工具用到.  <b>LIBXML_RPATH</b> 不被使用,所以没有被列出.
</p>

<p>这些变"exported"到autoconf外后,下面就要把他们加入到Makefiles里面了. 幸运的是,这个很简单的多.</p>

<p>如果工程子目录中有文件要使用这些头headers和libraries, 那么在<b>Makefile.am</b>.中要替换这些行:
<pre>
# set the include path found by configure
INCLUDES= $(all_includes)

# the library search path.
myAppName_LDFLAGS = $(all_libraries)
</pre>

替换为:

<pre>
# set the include path found by configure
INCLUDES= $(all_includes) $(LIBXML_CFLAGS)

# the library search path.
myAppName_LDFLAGS = $(all_libraries) $(LIBXML_LIBS)
</pre>

<p>再提示下,如果你用是2.x的版本, 这些改动会被覆盖掉, 而版本3, 你可以用automake manager或者手工来
改设置都没问题.</p>

<p>最后一步就是运行<b>make</b>, 根据依赖的复杂程度,你可以运行<b>make -f admin/Makefile.common</b>,
它能强迫autoconf重新更新文件和KDevelop认为要做的事情,比如说版本检查被改动了,那就要删除config.cache文件,重新运行
configure. </p>

<p>祝贺下,你现在已经知道在KDevelop里和外面,如何做libxml2的依赖检查和软件打包工作</p>

<p>更全面的介绍,请看<a href="http://www.amath.washington.edu/~lf/tutorials/autoconf/toolsmanual_toc.html">
this tutorial</a>.</p>

<p align=right><tt>Author: Christopher W. Curtis (ccurtis0 at yahoo dot com), last changed: 03-11-2003</tt></p>
<?php module_tail();?>
